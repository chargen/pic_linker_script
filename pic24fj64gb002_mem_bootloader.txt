
OUTPUT_ARCH("24FJ64GB002")
CRT0_STARTUP(crt0_standard.o)
CRT1_STARTUP(crt1_standard.o)

#ifdef BOOTLOADER
	#define BOOTLOADER_PROGRAM program
	#define APP_PROGRAM app_program
#elif defined(BOOTLOADER_APP)
	#define BOOTLOADER_PROGRAM bootloader_program
	#define APP_PROGRAM program
#else
	#error "Must define BOOTLOADER or BOOTLOADER_APP to use this linker script"
#endif

#define CHIP_PROGRAM_START 0x200
#define BOOTLOADER_SIZE 0x1000
#define CHIP_FLASH_TOP_ADDR 0xa7f8
#define IVT_MAP_SIZE 0x100

#define IVT_MAP_BASE ((CHIP_PROGRAM_START) + (BOOTLOADER_SIZE))
#define APP_BASE ((IVT_MAP_BASE) + (IVT_MAP_SIZE))
#define APP_LENGTH ((CHIP_FLASH_TOP_ADDR) - (APP_BASE))

#define IVT_BASE 0x4
#define ALT_IVT_BASE 0x104
#define RESET_BASE 0x0
#define DATA_BASE 0x800
#define CONFIG1_ADDR 0xabfe
#define CONFIG2_ADDR 0xabfc
#define CONFIG3_ADDR 0xabfa
#define CONFIG4_ADDR 0xabf8

MEMORY {
	/* Program Memory Space (Flash) */
#if defined(BOOTLOADER)
	reset : ORIGIN = RESET_BASE, LENGTH = 4
	ivt : ORIGIN = IVT_BASE, LENGTH = 0xfc
	res1 : ORIGIN = 0x100, LENGTH = 4
	aivt : ORIGIN = ALT_IVT_BASE, LENGTH = 0xfc
#endif
	BOOTLOADER_PROGRAM : ORIGIN = CHIP_PROGRAM_START, LENGTH = BOOTLOADER_SIZE
	ivt_map : ORIGIN = IVT_MAP_BASE, LENGTH = IVT_MAP_SIZE
	APP_PROGRAM : ORIGIN = APP_BASE, LENGTH = APP_LENGTH

	CONFIG1 : ORIGIN = CONFIG1_ADDR, LENGTH = 0x2
	CONFIG2 : ORIGIN = CONFIG2_ADDR, LENGTH = 0x2
	CONFIG3 : ORIGIN = CONFIG3_ADDR, LENGTH = 0x2
	CONFIG4 : ORIGIN = CONFIG4_ADDR, LENGTH = 0x2

	/* Data Space (RAM) */
	data : ORIGIN = DATA_BASE, LENGTH = 0x2000
};

__NO_HANDLES = 1;
__DATA_BASE = DATA_BASE;

__CONFIG1 = CONFIG1_ADDR;
__CONFIG2 = CONFIG2_ADDR;
__CONFIG3 = CONFIG3_ADDR;
__CONFIG4 = CONFIG4_ADDR;
